import org.jetbrains.kotlin.gradle.tasks.KaptGenerateStubs

group 'com.nextstory.nextstory'
version '1.0.0'

buildscript {
  def localProperties = rootProject.file('local.properties').with {
    def properties = new Properties()
    if (exists()) {
      withReader('UTF-8') {
        properties.load(it)
      }
    }
    return properties
  }

  ext {
    minSdk = 23
    targetSdk = 34
    compileSdk = 34

    kotlinVersion = '1.9.22'

    flutterVersionCode = localProperties.getProperty('flutter.versionCode') ?: '1'
    flutterVersionName = localProperties.getProperty('flutter.versionName') ?: '1.0'
    flutterRoot = localProperties.getProperty('flutter.sdk') ?: ''
  }

  repositories {
    google()
    mavenCentral()
  }

  dependencies {
    classpath 'com.android.tools.build:gradle:8.2.2'
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
  }
}

allprojects {
  repositories {
    google()
    mavenCentral()
  }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

android {
  namespace 'com.nextstory.nextstory'
  compileSdk rootProject.compileSdk

  defaultConfig {
    minSdk rootProject.minSdk
    targetSdk rootProject.targetSdk
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  kotlinOptions {
    jvmTarget = JavaVersion.VERSION_1_8.toString()
  }

  sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
  }

  lintOptions {
    checkAllWarnings true
    warningsAsErrors true
    disable 'AndroidGradlePluginVersion', 'InvalidPackage', 'GradleDependency'
  }
}

tasks.withType(KaptGenerateStubs).configureEach {
  // noinspection GrDeprecatedAPIUsage
  kotlinOptions {
    jvmTarget = JavaVersion.VERSION_1_8.toString()
  }
}

dependencies {
  coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.0.4'

  implementation platform("org.jetbrains.kotlin:kotlin-bom:$kotlinVersion")
  implementation 'org.jetbrains.kotlin:kotlin-stdlib'
  implementation 'androidx.core:core-ktx:1.12.0'

  constraints {
    // noinspection ForeignDelegate
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk7'

    // noinspection ForeignDelegate
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
  }
}
